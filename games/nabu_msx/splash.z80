; Splash screen routines
;
; Copyright (c) 2024 Brian Johnson.  All rights reserved.
;
; Redistribution and use in source and binary forms, with or without
; modification, are permitted provided that the following conditions
; are met:
; 1. Redistributions of source code must retain the above copyright
;    notice, this list of conditions and the following disclaimer.
; 2. Redistributions in binary form must reproduce the above copyright
;    notice, this list of conditions and the following disclaimer in the
;    documentation and/or other materials provided with the distribution.
;
; THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
; IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
; OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
; IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
; INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
; NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
; DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
; THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
; (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
; THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

.extern inigrp, ldirvm, filvrm, msx_font

.section .text.splash, "acrx"
.global splash
splash:
; Initialize Screen Mode 2
	call   inigrp
; Load MSX Font Patterns
	ld     hl, msx_font
	ld     de, 0x0000
	ld     bc, 0x0800
	call   ldirvm
	ld     hl, msx_font
	ld     de, 0x0800
	ld     bc, 0x0800
	call   ldirvm
	ld     hl, msx_font
	ld     de, 0x1000
	ld     bc, 0x0800
	call   ldirvm
; Color Table - red text on black background
	ld     a, (splash_color)
	ld     hl, 0x2000
	ld     bc, 0x1800
	call   filvrm
; Display splash screen message
	ld     ix, splash_msg
	call   display_message
	ld     ix, libmsx_splash
	call   display_message
delay:
; Delay Loop
	xor    a
	ld     b, 0x0a
	ld     d, a
	ld     e, a
delay_loop:
	dec    de
	ld     a, d
	or     e
	jr     nz, delay_loop
	djnz   delay_loop
	ret

display_message:
	ld     bc, 0
next_line:
	ld     a, (ix + 2)
	cp     0
	ret    z
	ld     c, a
	ld     de, 0x1800
	ld     h, 0
	ld     l, (ix + 1)
	ld     a, (ix + 0)
	add    hl, hl
	add    hl, hl
	add    hl, hl
	add    hl, hl
	add    hl, hl
	add    a, l
	ld     l, a
	adc    a, h
	sub    l
	ld     h, a
	add    hl, de
	ex     de, hl
	inc    ix
	inc    ix
	inc    ix
	push   ix
	pop    hl
	push   bc
	call   ldirvm
	pop    bc
	add    ix, bc
	jr     next_line
	ret

.section .rodata.splash, "adr"
.weak splash_color
splash_color:
	.byte 0x60
.weak splash_msg
splash_msg:
	.byte 0, 0, 0
libmsx_splash:
	.byte 6, 23, 20, "Created using libmsx"
	.byte 0, 0, 0
